#Использовать reflector

Перем ТипПровайдера Экспорт; // Файл, Произвольный
Перем Приоритет Экспорт; // Число
Перем Идентификатор Экспорт; // Строка
Перем Включен Экспорт; // Булево

Перем КлассПровайдера;
Перем Настройки;
Перем РеализованИнтерфейсЗаписи;
Перем РеализованИнтерфейсЧтения;

Функция ПрочитатьПараметры(Знач НастройкиПровайдера) Экспорт

	Если Не Включен Тогда
		Возврат Новый Соответствие;
	КонецЕсли;

	Если НЕ РеализованИнтерфейсЧтения Тогда
		Возврат Новый Соответствие;
	КонецЕсли;

	Возврат КлассПровайдера.ПрочитатьПараметры(НастройкиПровайдера);
	
КонецФункции

Процедура Отключить() Экспорт
	Включен = Ложь;
КонецПроцедуры

Процедура Включить() Экспорт
	Включен = Истина;
КонецПроцедуры

Процедура УстановитьПриоритет(НовыйПриоритет) Экспорт
	Приоритет = НовыйПриоритет;
КонецПроцедуры

Процедура ЗаписатьПараметры(Знач НастройкиПровайдера) Экспорт

	Если Не Включен Тогда
		Возврат;
	КонецЕсли;

	Если НЕ РеализованИнтерфейсЗаписи Тогда
		Возврат;
	КонецЕсли;

	КлассПровайдера.ЗаписатьПараметры(НастройкиПровайдера);
	
КонецПроцедуры

Процедура ПриСозданииОбъекта(Знач ВходящийКлассПровайдера)
	
	ИнтерфейсПровайдера = Новый ИнтерфейсОбъекта;
	ИнтерфейсПровайдера.Ф("Приоритет");
	ИнтерфейсПровайдера.Ф("Идентификатор");
	ИнтерфейсПровайдера.Ф("ТипПровайдера");
	// ИнтерфейсПровайдера.П("ОписаниеПровайдера", 1); //TODO: Определить надо ли??

	ИнтерфейсЧтения = Новый ИнтерфейсОбъекта;
	ИнтерфейсЧтения.Ф("ПрочитатьПараметры", 1);

	ИнтерфейсЗаписи = Новый ИнтерфейсОбъекта;
	ИнтерфейсЗаписи.Ф("ЗаписатьПараметры", 1);

	РефлекторОбъекта = Новый РефлекторОбъекта(ВходящийКлассПровайдера);
	
	РеализованИнтерфейсЧтения = РефлекторОбъекта.РеализуетИнтерфейс(ИнтерфейсЧтения);
	РеализованИнтерфейсЗаписи = РефлекторОбъекта.РеализуетИнтерфейс(ИнтерфейсЗаписи);
	РеализованИнтерфейсПровайдера = РефлекторОбъекта.РеализуетИнтерфейс(ИнтерфейсПровайдера);

	ПроверкаПровайдера = РеализованИнтерфейсПровайдера 
							И (РеализованИнтерфейсЧтения 
							ИЛИ РеализованИнтерфейсЗаписи);
	
	Если НЕ ПроверкаПровайдера Тогда
		// TODO: Добавить описание того что реализовано а что нет
		ВызватьИсключение "Не реализован необходимый интерфейс провайдера";
	КонецЕсли;

	ТипПровайдера = ВходящийКлассПровайдера.ТипПровайдера();
	
	Если ЭтоФайловыйПровайдер() Тогда
		КлассПровайдера = Новый ФайловыйПровайдерПараметров(ВходящийКлассПровайдера);	
	Иначе
		КлассПровайдера = ВходящийКлассПровайдера;
	КонецЕсли;
	
	Приоритет = КлассПровайдера.Приоритет();
	Идентификатор = КлассПровайдера.Идентификатор();
	Включен = Истина;

КонецПроцедуры

Функция ЭтоФайловыйПровайдер() Экспорт

	Возврат ВРег(ТипПровайдера) = ВРег("Файл")
		ИЛИ ВРег(ТипПровайдера) = ВРег("file");

КонецФункции