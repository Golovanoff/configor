#Использовать json

Перем мЧтениеJSON;
Перем мПрочитанныеПараметры;
Перем мОшибкиЧтения;

// Читает параметры из переданного объекта
//
// Параметры:
//  ПутьКФайлу   - Строка, Файл, Массив, Структура, Соответствие из строк и файлов - перечень путей к файлу или файлов
//					 из которых нужно прочитать параметры
//	пОшибкиЧтения - Соответствие - имя файла и описание ошибки, если не удалось прочитать параметры
//
// Возвращаемое значение:
//   Соответствие   - Параметры, прочитанные в соответствие
//
Функция Прочитать(Знач ПутьКФайлу, пОшибкиЧтения = Неопределено) Экспорт
	
	массивФайловДляЧтения = Новый Массив;

	мЧтениеJSON = Новый ПарсерJSON;
	мОшибкиЧтения = Новый Соответствие;
	мПрочитанныеПараметры = Новый Соответствие;

	ПрочитатьФайл( ПутьКФайлу );
	
	ВыполнитьПодстановки();

	пОшибкиЧтения = мОшибкиЧтения;

	Возврат мПрочитанныеПараметры;	
	
КонецФункции

Процедура ПрочитатьФайл( Знач пПолныйПутьКЧитаемомуФайлу )
	
	Если Не ФайлСуществует( пПолныйПутьКЧитаемомуФайлу ) Тогда			
		мОшибкиЧтения.Вставить( пПолныйПутьКЧитаемомуФайлу, "Не существует");
		Возврат;			
	КонецЕсли;
	
	Попытка
		ТекстФайла = ПолучитьТекстИзФайла( пПолныйПутьКЧитаемомуФайлу );
	Исключение
		мОшибкиЧтения.Вставить( пПолныйПутьКЧитаемомуФайлу, "Не удалось прочитать текст. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Попытка
		текстФайлаБезКомментариев = ВырезатьКомментарии( текстФайла );
		параметрыИзФайла = мЧтениеJSON.ПрочитатьJSON(текстФайлаБезКомментариев,,,Истина);
	Исключение
		мОшибкиЧтения.Вставить( пПолныйПутьКЧитаемомуФайлу, "Не удалось прочитать JSON. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	ПрочитатьПараметрыРекурсивно( параметрыИзФайла, пПолныйПутьКЧитаемомуФайлу );
	
КонецПроцедуры

Процедура ПрочитатьПараметрыРекурсивно( Знач пПараметры, Знач пПолныйПутьКЧитаемомуФайлу )
	
	Для каждого цЭлемент Из пПараметры Цикл
		
		Если ТипЗнч( цЭлемент.Значение ) = Тип("Структура")
			ИЛИ ТипЗнч( цЭлемент.Значение ) = Тип("Соответствие") Тогда
			
			ПрочитатьПараметрыРекурсивно( цЭлемент.Значение, пПолныйПутьКЧитаемомуФайлу );
			
		Иначе
			
			мПрочитанныеПараметры.Вставить( цЭлемент.Ключ, цЭлемент.Значение );
			ПрочитатьФайлИзЗначенияПараметра( цЭлемент.Ключ, цЭлемент.Значение, пПолныйПутьКЧитаемомуФайлу );
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочитатьФайлИзЗначенияПараметра( Знач пКлюч, Знач пЗначение, Знач пПолныйПутьКРодительскомуФайлу = "" )
	
	Если Не СтрНачинаетсяС( ВРег( пКлюч ), ВРег( Префикс_ПрочитатьФайл()) ) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНачинаетсяС( пЗначение, "." )
		И Не пПолныйПутьКРодительскомуФайлу = "" Тогда
		
		файл = Новый Файл( пПолныйПутьКРодительскомуФайлу );
		путьКФайлу = ОбъединитьПути( файл.Путь, пЗначение );
		
	Иначе

		путьКФайлу = пЗначение;

	КонецЕсли;
	
	ПрочитатьФайл( путьКФайлу );
	
КонецПроцедуры

Процедура ВыполнитьПодстановки()
	
	количествоПопыток = 10;
	количествоЭлементовСПодстановкой = мПрочитанныеПараметры.Количество();

	Для ц = 1 По количествоПопыток Цикл

		массивЭлементов = Новый Массив;
		Для каждого цЭлемент Из мПрочитанныеПараметры Цикл

			Если ТипЗнч( цЭлемент.Значение ) = Тип( "Строка")
				И СтрНайти( цЭлемент.Значение, "%" ) > 0 Тогда
				массивЭлементов.Добавить(цЭлемент);
			КонецЕсли;

		КонецЦикла;

		Если количествоЭлементовСПодстановкой = массивЭлементов.Количество()
			И Не количествоЭлементовСПодстановкой = мПрочитанныеПараметры.Количество() Тогда
			Прервать;
		КонецЕсли;

		количествоЭлементовСПодстановкой = массивЭлементов.Количество();

		Для каждого цЭлемент Из массивЭлементов Цикл

			ВыполнитьПодстановкуЭлементу(цЭлемент);

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьПодстановкуЭлементу( пЭлемент )

	Если Не ТипЗнч( пЭлемент.Значение ) = Тип( "Строка") Тогда
		Возврат;		
	КонецЕсли;

	результат = пЭлемент.Значение;

	регулярноеВыражение = Новый РегулярноеВыражение( "%([^%]*)%" );

	массивСовпадений = регулярноеВыражение.НайтиСовпадения( результат );

	Для каждого цЭлемент Из массивСовпадений Цикл

		ключ = СтрЗаменить( цЭлемент.Значение, "%", "" );

		найденноеЗначение = мПрочитанныеПараметры[ключ];

		Если Не найденноеЗначение = Неопределено Тогда
			результат = СтрЗаменить( результат, "%" + ключ + "%", найденноеЗначение );
		КонецЕсли;

	КонецЦикла;

	Если Не результат = пЭлемент.Значение Тогда
		мПрочитанныеПараметры.Вставить( пЭлемент.Ключ, результат );
	КонецЕсли;

КонецПроцедуры

Функция Префикс_ПрочитатьФайл()
	Возврат "ReadFile";
КонецФункции

Функция ФайлСуществует( Знач пПутьКФайлу )
	
	файл = Новый Файл( пПутьКФайлу );
	Возврат файл.Существует() И файл.ЭтоФайл();

КонецФункции

Функция ПолучитьТекстИзФайла( Знач пИмяФайла )
	
	прочитанныйТекст = "";
	чтениеТекста = Новый ЧтениеТекста(пИмяФайла, КодировкаТекста.UTF8);
	прочитанныйТекст = чтениеТекста.Прочитать();
	чтениеТекста.Закрыть();
	возврат прочитанныйТекст;

КонецФункции

// Удаляет все комментарии // и блоки /* */
Функция ВырезатьКомментарии( Знач пТекст )
	
	регулярноеВыражение = Новый РегулярноеВыражение( "(@(?:""[^""]*"")+|""(?:[^""\n\\]+|\\.)*""|'(?:[^'\n\\]+|\\.)*')|//.*|/\*(?s:.*?)\*/" );
	
	ЗначениеБезКомментариев = регулярноеВыражение.Заменить(пТекст, "$1" );

	Возврат ЗначениеБезКомментариев;

КонецФункции