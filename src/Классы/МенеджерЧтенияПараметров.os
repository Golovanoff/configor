#Использовать asserts
#Использовать logos
#Использовать tempfiles

Перем МассивПутейПоиска;
Перем ПутьКФайлуПараметров;
Перем ИмяФайлаПараметров;
Перем РасширениеФайлаПараметров;
Перем КлассЧтенияФайлаПараметров;
Перем ЧтениеФайлаПараметровВыполнено; // булево - флаг, что чтение выполнено

Перем ИндексПараметров; // Соответствие - плоский индекс всех параметров
Перем ПрочитанныеПараметры; // Соответствие - результат чтения из файла

Перем КлассПриемникПараметров; // Произвольный класс обеспечивающий: Функция Параметры() Экспорт, Процедура УстановитьПараметры(НовыеПараметры) Экспорт

Перем Лог;

Процедура ПриСозданииОбъекта()

	МассивПутейПоиска = Новый Массив;

	ДобавитьСтандартныеПутиПоиска();

	ИндексПараметров = Новый Соответствие;
	ПрочитанныеПараметры = Новый Структура;

	ЧтениеФайлаПараметровВыполнено = Ложь;

КонецПроцедуры

Процедура УстановитьФайлПараметров(Знач ПутьКФайлу) Экспорт

	ПутьКФайлуПараметров = ПутьКФайлу;
	ЧтениеФайлаПараметровВыполнено = Ложь;

КонецПроцедуры

Процедура ДобавитьПутьПоиска(Знач ПутьПоискаФайлов) Экспорт

	МассивПутейПоиска.Добавить(ПутьПоискаФайлов);

КонецПроцедуры

//
Функция ИспользуемыйФайлПараметров() Экспорт
	Возврат ПолучитьПутьКФайлуПараметров();
КонецФункции

Процедура УстановитьИмяФайла(Знач ИмяФайла) Экспорт

	ИмяФайлаПараметров = ИмяФайла;
	ЧтениеФайлаПараметровВыполнено = Ложь;

КонецПроцедуры

Процедура УстановитьРасширениеФайла(Знач РасширениеФайла, Знач КлассЧтенияФайла = Неопределено) Экспорт

	РасширениеФайлаПараметров = РасширениеФайла;

	Если Не КлассЧтенияФайла = Неопределено Тогда

		КлассЧтенияФайлаПараметров = КлассЧтенияФайла;

	КонецЕсли;

КонецПроцедуры

Процедура УстановитьКлассПриемник(КлассПараметров) Экспорт

	КлассПриемникПараметров = КлассПараметров;

КонецПроцедуры

Процедура Прочитать() Экспорт

	НайтиИУстановитьФайлПараметров();

	ПрочитанныеПараметры = КлассЧтенияФайлаПараметров.Прочитать(ПутьКФайлуПараметров);

	Лог.Отладка("ПрочитанныеПараметры количество <%1>", ПрочитанныеПараметры.Количество());

	ОбновитьИндексПараметров();

	ЧтениеФайлаПараметровВыполнено = Истина;

КонецПроцедуры

Процедура ОбновитьИндексПараметров()

	// Рекурсивный вызов для входящих параметров
	ДобавитьЗначениеПараметра("", ПрочитанныеПараметры);

КонецПроцедуры

Функция ЭтоМассив(Знач Значение)
	Возврат ТипЗнч(Значение) = Тип("Массив");
КонецФункции

Функция ЭтоСоответствие(Знач Значение)
	Возврат ТипЗнч(Значение) = Тип("Соответствие");
КонецФункции

Функция ЭтоСтруктура(Знач Значение)
	Возврат ТипЗнч(Значение) = Тип("Структура");
КонецФункции

Процедура ДобавитьПараметрВИндекс(Знач ИмяПараметра, Знач ЗначениеПараметра)

	// Вставляем все значение целиком
	// Для получения массивов и соответствий сразу
	Если Не ПустаяСтрока(ИмяПараметра) Тогда
		Лог.Отладка("Добавляю параметр <%1> со значением <%2> в индекс", ИмяПараметра, ЗначениеПараметра);
		ИндексПараметров.Вставить(ИмяПараметра, ЗначениеПараметра);
	КонецЕсли;

	// Рекурсивное заполнение значения параметра
	ДобавитьЗначениеПараметра(ИмяПараметра, ЗначениеПараметра);

КонецПроцедуры

Процедура ДобавитьЗначениеПараметра(Знач ИмяПараметра, Знач ЗначениеПараметра)

	Если ЭтоМассив(ЗначениеПараметра) Тогда
		ДобавитьПараметрМассивВИндекс(ЗначениеПараметра, ИмяПараметра);
	ИначеЕсли ЭтоСоответствие(ЗначениеПараметра) Тогда
		ДобавитьСоответствиеВИндекс(ЗначениеПараметра, ИмяПараметра);
	ИначеЕсли ЭтоСтруктура(ЗначениеПараметра) Тогда
		ДобавитьСоответствиеВИндекс(ЗначениеПараметра, ИмяПараметра);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПараметрМассивВИндекс(Знач МассивЗначений, Знач ИмяРодителя = "")

	Лог.Отладка("Обрабатываю массив значений <%1> ", ИмяРодителя);

	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для ИндексЗначения = 0 По МассивЗначений.ВГраница() Цикл

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, ИндексЗначения);
		ДобавитьПараметрВИндекс(ИмяПараметра, МассивЗначений[ИндексЗначения]);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьСоответствиеВИндекс(Знач ВходящиеПараметры, Знач ИмяРодителя = "")

	Лог.Отладка("Обрабатываю соответствие значений <%1> ", ИмяРодителя);

	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для каждого КлючЗначение Из ВходящиеПараметры Цикл

		Лог.Отладка("Обрабатываю соответствие ключ <%1> ", КлючЗначение.Ключ);

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, КлючЗначение.Ключ);
		ЗначениеПараметра = КлючЗначение.Значение;

		ДобавитьПараметрВИндекс(ИмяПараметра, ЗначениеПараметра);

	КонецЦикла;

КонецПроцедуры

Процедура НайтиИУстановитьФайлПараметров()

	Если ЗначениеЗаполнено(ПутьКФайлуПараметров) Тогда

		НайденныйФайл = Новый Файл(ПутьКФайлуПараметров);

	Иначе
		
		Для каждого ПутьПоиска Из МассивПутейПоиска Цикл

			// TODO Реализовать поиск файла по имени и расширению
			НайденныйФайл = "";

		КонецЦикла;

	КонецЕсли;

	Лог.Отладка("Использую файл параметров <%1>", НайденныйФайл.ПолноеИмя);

	Если РасширениеФайлаПараметров = НайденныйФайл.Расширение Тогда
		КлассЧтенияФайла = КлассЧтенияФайлаПараметров;
	Иначе
		КлассЧтенияФайла = ПолучитьОбработчикПоРасширению(НайденныйФайл.Расширение);
	КонецЕсли;

	УстановитьФайлПараметров(НайденныйФайл.ПолноеИмя);

	РасширениеФайлаПараметров = НайденныйФайл.Расширение;
	КлассЧтенияФайлаПараметров = КлассЧтенияФайла;

КонецПроцедуры

Функция Параметр(Знач ИмяПараметра, Знач ЗначениеПоУмолчанию = Неопределено) Экспорт

	СообщениеОшибки = "Получение значений возможно только после вызова метода Прочитать";
	Ожидаем.Что(ЧтениеФайлаПараметровВыполнено, СообщениеОшибки).ЭтоИстина();

	ЗначениеИзИндекса = ИндексПараметров[ИмяПараметра];

	Если НЕ ЗначениеИзИндекса = Неопределено Тогда
		Возврат ЗначениеИзИндекса;
	КонецЕсли;

	Возврат ЗначениеПоУмолчанию;

КонецФункции

Функция ПолучитьОбработчикПоРасширению(Знач РасширениеФайла)

	Лог.Отладка("Получаю класс чтения файла по расширению <%1>", РасширениеФайла);

	Если НРег(РасширениеФайла) = ".yaml" Тогда
		Возврат Новый ЧтениеПараметровYAML();
	ИначеЕсли НРег(РасширениеФайла) = ".json" Тогда
		Возврат Новый ЧтениеПараметровJSOn();
	Иначе
		ВызватьИсключение "Расширение данного файла не поддерживаться для чтения";
	КонецЕсли;

КонецФункции

Процедура ПроверитьКлассЧтенияФайлаПараметров()

	НеобходимыйИнтерфейсЕсть = ПроверитьМетодКласса(КлассЧтенияФайлаПараметров, "Прочитать", 1);

	Если НЕ НеобходимыйИнтерфейсЕсть Тогда
		ВызватьИсключение СтрШаблон("Класс <%1> не реализует необходимой функции <%2>", КлассЧтенияФайлаПараметров , "Прочитать(ИмяФайла)" );
	КонецЕсли;

КонецПроцедуры

Процедура ВыгрузитьПараметрыВКлассПриемник()

	СтруктураПараметров = КлассПриемникПараметров.ПолучитьПараметры();

	ЗаполнитьСтруктуруПараметров(СтруктураПараметров);

	КлассПриемникПараметров.УстановитьПараметры(СтруктураПараметров);

КонецПроцедуры

Процедура ДобавитьЗначениеВСтруктуру(СтруктураПриемник, Знач ИмяПараметра, Знач ЗначениеПараметра)


КонецПроцедуры

Процедура ДобавитьСоответствиеВСтруктуру(Знач ВходящиеПараметры, Знач ИмяРодителя = "")

	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для каждого КлючЗначение Из ВходящиеПараметры Цикл

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, КлючЗначение.Ключ);
		ЗначениеПараметра = КлючЗначение.Значение;

		ДобавитьПараметрВИндекс(ИмяПараметра, ЗначениеПараметра);

	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтруктуруПараметров(СтруктураПараметров, Знач ИмяРодителя = "")

	ЕстьОбработчикПолученияЗначения = Ложь; //  Проверить на доступный метод ПриПолученииЗначения(ИмяСвойства, ЗначениеСвойства, СтандартнаяОбработка)
	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для каждого КлючЗначение Из СтруктураПараметров Цикл

		ИмяКлюча = КлючЗначение.Ключ;
		ТекущееЗначение = КлючЗначение.Значение;

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, ИмяКлюча);

		НовоеЗначение = Параметр(ИмяПараметра);

		Если НовоеЗначение = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтандартнаяОбработка = Истина;

		Если СтандартнаяОбработка Тогда

			СтруктураПараметров[ИмяКлюча] = ОбработатьЗначениеРекурсивно(ТекущееЗначение, НовоеЗначение, ИмяКлюча);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ОбработатьЗначениеРекурсивно(Знач ЗначениеСтруктуры, Знач ЗначениеПараметра, Знач ИмяПараметра = "")

	НовоеЗначение = Параметр(ИмяПараметра);

	Если НовоеЗначение = Неопределено Тогда
		Возврат ЗначениеСтруктуры;
	КонецЕсли;

	Если ЭтоМассив(ЗначениеСтруктуры) Тогда
		Возврат ОбработатьЗначениеМассив(ЗначениеСтруктуры, ЗначениеПараметра, ИмяПараметра);
	ИначеЕсли ЭтоСоответствие(ЗначениеСтруктуры) Тогда
		Возврат ОбработатьЗначениеСоответствие(ЗначениеСтруктуры, ЗначениеПараметра, ИмяПараметра);
	ИначеЕсли ЭтоСтруктура(ЗначениеСтруктуры) Тогда
		Возврат ОбработатьЗначениеСтруктуру(ЗначениеСтруктуры, ЗначениеПараметра, ИмяПараметра);
	КонецЕсли;

КонецФункции

Функция ОбработатьЗначениеМассив(Знач ЗначениеСтруктуры, Знач МассивЗначений, Знач ИмяРодителя = "")

	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для ИндексЗначения = 0 По МассивЗначений.ВГраница() Цикл

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, ИндексЗначения);
		ОбработатьЗначениеРекурсивно(ЗначениеСтруктуры, ИмяПараметра, МассивЗначений[ИндексЗначения]);

	КонецЦикла;

КонецФункции

Функция ОбработатьЗначениеСоответствие(Знач ЗначениеСтруктуры, Знач МассивЗначений, Знач ИмяРодителя = "")

	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для ИндексЗначения = 0 По МассивЗначений.ВГраница() Цикл

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, ИндексЗначения);
		ОбработатьЗначениеРекурсивно(ЗначениеСтруктуры, ИмяПараметра, МассивЗначений[ИндексЗначения]);

	КонецЦикла;

КонецФункции

Функция ОбработатьЗначениеСтруктуру(Знач ЗначениеСтруктуры, Знач МассивЗначений, Знач ИмяРодителя = "")

	ШаблонИмениПараметра = "%1";

	Если Не ПустаяСтрока(ИмяРодителя) Тогда
		ШаблонИмениПараметра = ИмяРодителя + ".%1";
	КонецЕсли;

	Для ИндексЗначения = 0 По МассивЗначений.ВГраница() Цикл

		ИмяПараметра = СтрШаблон(ШаблонИмениПараметра, ИндексЗначения);
		ОбработатьЗначениеРекурсивно(ЗначениеСтруктуры, ИмяПараметра, МассивЗначений[ИндексЗначения]);

	КонецЦикла;

КонецФункции

Функция ПолучитьПутьКФайлуПараметров()

	ФайлПараметров = Новый Файл(ПутьКФайлуПараметров);

	Если ФайлПараметров.Существует() Тогда
		Возврат ФайлПараметров.ПолноеИмя;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция ПроверитьМетодКласса(Знач ПроверяемыйКласс,
	Знач ИмяМетода,
	Знач ТребуемоеКоличествоПараметров = 0,
	Знач ЭтоФункция = Ложь)

	РефлекторПроверкиКоманд = Новый Рефлектор;

	ЕстьМетод = РефлекторПроверкиКоманд.МетодСуществует(ПроверяемыйКласс, ИмяМетода);
	Лог.Отладка("Проверяемый метод <%1> найден: %2", ИмяМетода, ЕстьМетод);
	Если Не ЕстьМетод Тогда
		Возврат Ложь;
	КонецЕсли;

	ТаблицаМетодов = РефлекторПроверкиКоманд.ПолучитьТаблицуМетодов(ПроверяемыйКласс);

	СтрокаМетода = ТаблицаМетодов.Найти(ИмяМетода, "Имя");
	Лог.Отладка("Поиск строки в таблице методов класса <%1> найдена: %2, общее количество методов класса: %3", ПроверяемыйКласс, НЕ СтрокаМетода = Неопределено, ТаблицаМетодов.Количество());
	Если СтрокаМетода = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	ПроверкаНаФункцию = ЭтоФункция = СтрокаМетода.ЭтоФункция;
	ПроверкаНаКоличествоПараметров = ТребуемоеКоличествоПараметров = СтрокаМетода.КоличествоПараметров;

	Лог.Отладка("Проверяемый метод <%1> корректен: %2", ИмяМетода, ПроверкаНаФункцию И ПроверкаНаКоличествоПараметров);
	Возврат ПроверкаНаФункцию
		И ПроверкаНаКоличествоПараметров;

КонецФункции // ПроверитьМетодУКласса()

Процедура ДобавитьСтандартныеПутиПоиска()

	СистемнаяИнформация = Новый СистемнаяИнформация;

	МассивПутейПоиска.Добавить(ТекущийКаталог());
	МассивПутейПоиска.Добавить(СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ЛокальныйКаталогДанныхПриложений));
	//МассивПутейПоиска.Добавить(ТекущийКаталог());

КонецПроцедуры

Процедура ОчиститьСтандартныеПутиПоиска()

	МассивПутейПоиска.Очистить();

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("oscript.lib.configor");
